import ai.koog.gradle.publish.maven.Publishing.publishToMaven

plugins {
    id("ai.kotlin.multiplatform")
    alias(libs.plugins.kotlin.serialization)
}

val isAndroidEnabled = (findProperty("koog.target.android") as? String)?.toBoolean() ?: true
val isJsEnabled = (findProperty("koog.target.js") as? String)?.toBoolean() ?: true
val isIosEnabled = (findProperty("koog.target.ios") as? String)?.toBoolean() ?: true
val isWasmJsIncluded = (findProperty("koog.target.wasmJs") as? String)?.toBoolean() ?: true

kotlin {
    sourceSets {
        commonMain {
            dependencies {
                api(project(":http-client:http-client-core"))
                api(project(":prompt:prompt-llm"))
                api(project(":prompt:prompt-model"))
                api(project(":prompt:prompt-tokenizer"))
                api(project(":prompt:prompt-executor:prompt-executor-clients"))
                api(project(":embeddings:embeddings-base"))
                api(project(":prompt:prompt-structure"))

                api(libs.kotlinx.coroutines.core)
                implementation(libs.oshai.kotlin.logging)
            }
        }

        if (isAndroidEnabled) {
            androidUnitTest {
                dependencies {
                    implementation(libs.ktor.client.cio)
                }
            }
        }

        if (isIosEnabled) {
            appleTest {
                dependencies {
                    implementation(libs.ktor.client.darwin)
                }
            }
        }

        if (isJsEnabled) {
            jsTest {
                dependencies {
                    implementation(libs.ktor.client.js)
                }
            }
        }

        if (isWasmJsIncluded) {
            wasmJsTest {
                dependencies {
                    implementation(libs.ktor.client.cio)
                }
            }
        }

        commonTest {
            dependencies {
                implementation(project(":http-client:http-client-ktor"))
                implementation(project(":test-utils"))
                implementation(project(":agents:agents-features:agents-features-event-handler"))
                implementation(libs.kotlinx.coroutines.core)
                implementation(libs.kotlinx.coroutines.test)
                implementation(libs.ktor.client.mock)
            }
        }

        jvmTest {
            dependencies {
                implementation(project(":agents:agents-core"))
                implementation(project(":agents:agents-features:agents-features-event-handler"))
                implementation(project(":agents:agents-features:agents-features-trace"))
                implementation(libs.ktor.client.cio)
            }
        }
    }

    explicitApi()
}

publishToMaven()
